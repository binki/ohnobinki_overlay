diff -r 8a71bdce0044 -r cb1fbfdc8834 Makefile.am
--- /dev/null	Thu Jan 01 00:00:00 1970 +0000
+++ Makefile.am	Tue Jun 22 23:57:25 2010 -0400
@@ -0,0 +1,52 @@
+# $Id: $
+
+AM_CPPFLAGS = -I$(srcdir)/include
+
+bin_PROGRAMS =
+lib_LTLIBRARIES = libircclient.la
+include_HEADERS = \
+	include/libircclient.h \
+	include/libirc_errors.h \
+	include/libirc_events.h \
+	include/libirc_options.h \
+	include/libirc_rfcnumeric.h
+noinst_HEADERS = \
+	include/libirc_dcc.h \
+	include/libirc_doc.h \
+	include/libirc_doc_faq.h \
+	include/libirc_params.h \
+	include/libirc_session.h
+
+libircclient_la_CPPFLAGS = $(AM_CPPFLAGS) -DIN_BUILDING_LIBIRC
+libircclient_la_SOURCES = src/libircclient.c
+EXTRA_DIST = \
+	src/colors.c \
+	src/dcc.c \
+	src/errors.c \
+	src/portable.c \
+	src/sockets.c \
+	src/utils.c
+
+if ENABLE_EXAMPLES
+bin_PROGRAMS += \
+	censor \
+	colors \
+	ircftp \
+	irctest \
+	spammer
+endif
+
+censor_SOURCES = examples/censor.cpp
+censor_LDADD = libircclient.la
+
+colors_SOURCES = examples/colors.cpp
+colors_LDADD = libircclient.la
+
+ircftp_SOURCES = examples/ircftp.cpp
+ircftp_LDADD = libircclient.la
+
+irctest_SOURCES = examples/irctest.c
+irctest_LDADD = libircclient.la
+
+spammer_SOURCES = examples/spammer.c
+spammer_LDADD = libircclient.la
diff -r 8a71bdce0044 -r cb1fbfdc8834 configure.ac
--- /dev/null	Thu Jan 01 00:00:00 1970 +0000
+++ configure.ac	Tue Jun 22 23:57:25 2010 -0400
@@ -0,0 +1,84 @@
+#                                               -*- Autoconf -*-
+# Process this file with autoconf to produce a configure script.
+
+AC_PREREQ(2.52)
+AC_INIT([libircclient], [1.3], [gyunaev@ulduzsoft.com], [], [http://libircclient.sourceforge.net/])
+AC_CONFIG_SRCDIR([include/libircclient.h])
+AC_CONFIG_HEADER([include/config.h])
+
+AM_INIT_AUTOMAKE([foreign subdir-objects -Wall])
+LT_INIT
+
+# Check for command-line
+AC_ARG_ENABLE([debug],
+            [AS_HELP_STRING([--enable-debug],
+               [compile with debug information (no)])],
+            [enable_debug=$enableval],
+            [enable_debug=no])
+
+AC_ARG_ENABLE([threads],
+            [AS_HELP_STRING([--enable-threads],
+               [compile with multithread support (yes)])],
+            [enable_threads=$enableval],
+            [enable_threads=yes])
+
+AC_ARG_ENABLE([ipv6],
+            [AS_HELP_STRING([--enable-ipv6],
+               [compile with IPv6 support (no)])],
+            [enable_ipv6=$enableval],
+            [enable_ipv6=no])
+
+AC_ARG_ENABLE([examples],
+	[AS_HELP_STRING([--enable-examples],
+		[Compile example programs (yes)])],
+	[enable_examples=$enableval],
+	[enable_examples=yes])
+
+# Checks for programs.
+AC_PROG_CXX
+AM_PROG_CC_C_O
+
+# Checks for header files.
+AC_HEADER_STDC
+
+# Checks for typedefs, structures, and compiler characteristics.
+AC_HEADER_STDBOOL
+AC_C_CONST
+AC_TYPE_SIZE_T
+AC_HEADER_TIME
+
+# Checks for library functions.
+AC_FUNC_MALLOC
+AC_FUNC_SELECT_ARGTYPES
+AC_FUNC_STAT
+AC_CHECK_FUNCS([localtime_r socket])
+
+AC_CHECK_LIB([socket],[socket],
+	[AC_DEFINE([HAVE_SOCKET], [], [socket() is available])
+	LIBS="$LIBS -lsocket"])
+
+AS_IF([test "$enable_debug" = "yes"],
+	[AC_DEFINE([ENABLE_DEBUG], [], [Enable debugging codepaths])])
+
+if test "$enable_ipv6" = "yes"; then
+	AC_CHECK_FUNCS([getaddrinfo inet_pton])
+	AC_CHECK_LIB(nsl, getaddrinfo, AC_DEFINE([HAVE_GETADDRINFO], [], [getaddrinfo() is available]) LIBS="$LIBS -lnsl")
+	AC_DEFINE([ENABLE_IPV6], [], [Enable IPv6 support])
+else
+	AC_CHECK_FUNCS([gethostbyname_r inet_ntoa])
+	AC_CHECK_LIB([nsl], [inet_ntoa],
+		[AC_DEFINE([HAVE_INET_NTOA], [], [inet_ntoa() is available])
+		LIBS="$LIBS -lnsl"])
+fi
+
+AS_IF([test "$enable_threads" = "yes"],
+	[AC_SEARCH_LIBS([pthread_create], [pthread], [],
+		[AC_MSG_ERROR([You specified --enable-threads but I cannot find pthread_create().]) ])
+	AC_DEFINE([ENABLE_THREADS], [], [Enable threading support])
+	AC_DEFINE([_REENTRANT], [], [Necessary for threading support])
+	])
+
+AM_CONDITIONAL([ENABLE_EXAMPLES], [test "$enable_examples" = "yes"])
+
+AC_CONFIG_FILES([Makefile])
+AC_OUTPUT
