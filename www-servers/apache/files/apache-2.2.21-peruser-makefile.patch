From: Nathan Phillip Brink <binki@gentoo.org>
Subject: Fix peruser-v16's Makefile stuff to work with modern GNU
	make, moving the PERUSER_MAX_CPU_USAGE to
	--enable-peruser-max-cpu-usage.

--- a/server/mpm/experimental/peruser/Makefile.in
+++ b/server/mpm/experimental/peruser/Makefile.in
@@ -3,11 +3,9 @@
-### Remove '#' to enable
-# PERUSER_MAX_CPU_USAGE = 1
-
-.if defined(PERUSER_MAX_CPU_USAGE)
-LTLIBRARY_SOURCES = peruser.c cpu_usage.c
-LTLIBRARY_LIBADD = -lkvm
-.else
-LTLIBRARY_SOURCES = peruser.c
-.endif
+LTLIBRARY_SOURCES = peruser.c @PERUSER_CPU_USAGE_C@
+LTLIBRARY_LIBADD = @PERUSER_CPU_USAGE_KVM_LIBS@ @PERUSER_PAM_LIBS@
 
+top_srcdir = @top_srcdir@
+top_builddir = @top_builddir@
+srcdir = @srcdir@
+builddir = @builddir@
+VPATH = @abs_srcdir@
 include $(top_srcdir)/build/ltlib.mk
--- a/server/mpm/experimental/peruser/config.m4
+++ b/server/mpm/experimental/peruser/config.m4
@@ -1,3 +1,25 @@
+AC_ARG_ENABLE([peruser-max-cpu-usage],
+	[AS_HELP_STRING([--enable-peruser-max-cpu-usage], [Enable the MaxCPUUsage extension to the peruser MPM.])])
+AC_ARG_VAR([PERUSER_CPU_USAGE_KVM_LIBS], [If --enable-peruser-max-cpu-usage is specified, the flags to link against libkvm])
+AS_IF([test "x$enable_peruser_max_cpu_usage" = "xyes"],
+	[PERUSER_CPU_USAGE_C="cpu_usage.c"
+	PERUSER_CPU_USAGE_KVM_LIBS="${PERUSER_CPU_USAGE_KVM_LIBS--lkvm}"
+	AC_DEFINE([PERUSER_MAX_CPU_USAGE], [], [Define to enable the MaxCPUUsage extension to mpm_peruser])],
+	[PERUSER_CPU_USAGE_C=""
+	PERUSER_CPU_USAGE_KVM_LIBS=""])
+AC_SUBST([PERUSER_CPU_USAGE_C])
+AC_SUBST([PERUSER_CPU_USAGE_KVM_LIBS])
+
+AC_ARG_ENABLE([peruser-pam],
+	[AS_HELP_STRING([--enable-peruser-pam], [Enable the PAM session loading extension for the peruser MPM.])])
+AC_ARG_VAR([PERUSER_PAM_LIBS], [If --enable-peruser-pam is specified, the flags to link against libpam.])
+AS_IF([test "x$enable_peruser_max_cpu_usage" = "xyes"],
+	[AC_DEFINE([PERUSER_PAM_SESSION], [], [Define to enable the PAM session extension for mpm_peruser])
+	PERUSER_PAM_LIBS="${PERUSER_PAM_LIBS--lpam}"],
+	[PERUSER_PAM_LIBS=""])
+AC_SUBST([PERUSER_PAM_LIBS])
+
 if test "$MPM_NAME" = "peruser" ; then
-    APACHE_FAST_OUTPUT(server/mpm/experimental/$MPM_NAME/Makefile)
+    AC_CONFIG_FILES([server/mpm/experimental/$MPM_NAME/Makefile])
+    touch server/mpm/experimental/$MPM_NAME/.deps
 fi
